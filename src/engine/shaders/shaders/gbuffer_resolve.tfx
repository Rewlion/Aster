#include "atmosphere/atmosphere.inc.tfx"
#include "atmosphere/sph.inc.tfx"
#include "depth.inc.tfx"
#include "frame.inc.tfx"
#include "materials.inc.tfx"
#include "math.inc.tfx"
#include "pbr.inc.tfx"
#include "screen_space.inc.tfx"
#include "color_spaces.inc.tfx"

scope ResolveGbufferScope {
  reserve:
    descriptor_set(2);
    register(buffer, 0-6);
    register(texture, 0-10);

  RWStructuredBuffer<float3> atmParamsBuffer = extern(atmParamsBuffer);
  
  RWTexture2D<float4> resolveTarget = extern(resolveTarget);
  Texture2D gbufferAlbedo = extern(gbuffer_albedo);
  Texture2D gbufferNormal = extern(gbuffer_normal);
  Texture2D gbufferMetalRoughness = extern(gbuffer_metal_roughness);
  Texture2D gbufferDepth = extern(gbuffer_depth);

  //SSSR
  Texture2D specularLight = extern(specularLight);

  #hlsl
  #include "clustered_light/utils.hlsl"
  #end

  //Clustered light
  StructuredBuffer<Light> lightBuffer = extern(clustersLightBuffer);
  StructuredBuffer<ClusterInfo> clustersInfoBuffer = extern(clustersInfoBuffer);
  StructuredBuffer<ClusterIndirection> indirectionBuffer = extern(clustersIndirectionBuffer);

  //GI
  #hlsl
  #include <gi_surfels/surfel.hlsl>
  #include <gi_surfels/spatial_storage.hlsl>
  #end
  RWStructuredBuffer<SurfelData> surfelsStorage = extern(surfelsStorage);
  StructuredBuffer<uint> surfelsSpatialStorage = extern(surfelsSpatialStorage);
};

technique ResolveGbuffer {
  activate scope ResolveGbufferScope;
  support scope FrameScope;

  render_state:
    primitive_topology = triangle_strip;
    depth:
     test = no;
     write = no;

  USE_MATH();
  #hlsl
  #include "atmosphere/sph.hlsl"
  #end
  USE_ATMOSPHERE_PARAMS();
  USE_ATM_SPH();

  USE_DEPTH_UTILS();
  USE_PBR();
  USE_MATERIALS_UNPACKING();
  USE_COLOR_SPACES();

  #hlsl
    #include "clustered_light/lights_apply.hlsl"
    #include <gi_surfels/surfel_norm.hlsl>

    struct AccumulateIrradiance
    {
      float3 N;
      float3 worldPos;
      float3 irradiance;
      float accWeight;
      float totalAverageWeight;
      float3 totalIrradiance;

      void onSurfel(uint surfel_id)
      {
        SurfelData surfel = surfelsStorage[surfel_id];

        float ellipsoidNorm = calcSurfelEllipsoidNorm(worldPos, surfel);
        float normalWeight =  max(0, dot(N, surfel.normal));
        float weight = smoothstep(1.0, 0.0, ellipsoidNorm / surfel.radius) * normalWeight;

        irradiance += surfel.irradiance * weight;
        accWeight += weight;

        float averageWeight = max((ellipsoidNorm - surfel.radius), 0.00001);
        averageWeight = averageWeight*averageWeight;
        totalIrradiance += (surfel.irradiance * normalWeight) / (averageWeight);
        totalAverageWeight += 1.0 / (averageWeight);
      }

      float3 getResult()
      {
        if (accWeight <= 0.1)
        {
          return (totalIrradiance / totalAverageWeight);
        }
        return irradiance / max(accWeight, 0.001);
      }

      bool finish()
      {
        return false;
      }
    };

    float3 getDiffuseIndirectLight(float3 N, float3 world_pos, float3 camera_pos, float far_plane_dist)
    {
      AccumulateIrradiance accumulateIrradiance = {N, world_pos, 0.0.xxx, 0, 0, 0.xxx};

      float3 cameraToWorldPos = world_pos - camera_pos;

      ROSpatialStorage st = {surfelsSpatialStorage, far_plane_dist};
      st.forEachSurfelInCell(world_pos, cameraToWorldPos, accumulateIrradiance);

      return accumulateIrradiance.getResult();
    }

    [numthreads(8, 8, 1)]
    void cs_main(uint3 dispatch_thread_id: SV_DispatchThreadID)
    {
      uint2 renderSize;
      resolveTarget.GetDimensions(renderSize.x, renderSize.y);

      if (any(dispatch_thread_id.xy >= renderSize))
        return;

      uint3 tc = dispatch_thread_id;
      float depth = gbufferDepth.Load(tc).r;

      if (depth == 0.0)
        return;

      float3 leftRight = FrameScope.rtView_WS - FrameScope.ltView_WS;
      float3 topBot = FrameScope.lbView_WS - FrameScope.ltView_WS;

      float2 t = float2(tc.xy) / float2(renderSize - uint2(1, 1));
      float3 farPlainPoint = FrameScope.ltView_WS + leftRight * t.x + topBot * t.y;
      float3 viewDir = normalize(farPlainPoint);

      ProcessedMaterial mat = processMaterial(unpackMaterial(tc));

      float3 worldPos = calcWorldPos(FrameScope.cameraPos, farPlainPoint, depth, FrameScope.zNear_zFar.x, FrameScope.zNear_zFar.y);

      float3 N = mat.normal;
      float3 L = getAtmSunDir();
      float3 V = normalize(FrameScope.cameraPos - worldPos);
      float3 H = normalize(L+V);
      float3 R = reflect(-V,N);

      float NoL = max(dot(L, N), 0.0);
      float NoV = max(dot(V,N), 0.0);
      float VoH = max(dot(V,H), 0.0);
      float NoH = max(dot(N,H), 0.0);

      DynamicLightsApplyParams dynamicLightParams;
      dynamicLightParams.tc = tc.xy;
      dynamicLightParams.renderSize = renderSize;
      dynamicLightParams.worldPos = worldPos;
      dynamicLightParams.N = N;
      dynamicLightParams.NoV = NoV;
      dynamicLightParams.NoH = NoH;
      dynamicLightParams.VoH = VoH;
      dynamicLightParams.zView = linearize_z(depth, FrameScope.zNear_zFar.x, FrameScope.zNear_zFar.y);
      dynamicLightParams.zNear = FrameScope.zNear_zFar.x;
      dynamicLightParams.zFar = FrameScope.zNear_zFar.y;
      dynamicLightParams.roughness = mat.roughness;
      dynamicLightParams.ggxAlpha = mat.ggxAlpha;
      dynamicLightParams.specularReflection = mat.specularReflection;
      dynamicLightParams.clustersInfoBuffer = clustersInfoBuffer;
      dynamicLightParams.indirectionBuffer = indirectionBuffer;
      dynamicLightParams.lightBuffer = lightBuffer;

      float3 dynamicLightsDiffuseNoBRDF;
      float3 dynamicLightsSpecular;
      dynamicLights(dynamicLightParams, dynamicLightsDiffuseNoBRDF, dynamicLightsSpecular);

      float3 diffuseSPH = getSphDiffuseProbe(N);
      float3 diffuseIndirectLight = getDiffuseIndirectLight(N, worldPos, FrameScope.cameraPos, FrameScope.zNear_zFar.x);
      float3 sunL = float3(0,0,0);// getSunL() * NoL;

      float3 diffusePart = diffuseBRDF(mat.diffuseColor, mat.specularReflection, VoH) * (diffuseIndirectLight + sunL + dynamicLightsDiffuseNoBRDF);
      float3 specularPart = specularLight.Load(tc).rgb;

      float3 Lo = diffusePart + specularPart;

      Lo = diffusePart;
      Lo = getDiffuseIndirectLight(N, worldPos, FrameScope.cameraPos, FrameScope.zNear_zFar.x);
      
      resolveTarget[tc.xy] = float4(Lo, 1.0);
    }
  #end

  compile(cs, cs_main);
};