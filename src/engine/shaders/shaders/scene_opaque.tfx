#include "frame.inc.tfx"
#include "mesh.inc.tfx"
#include "normals.inc.tfx"
#include "materials.inc.tfx"

technique SceneOpaque {
  support scope FrameScope;
  support scope StaticModelScope;

  USE_STATIC_MESH();
  USE_NORMALS_UTILS();
  USE_MATERIALS_PACKING();

  #hlsl
    struct VsOutput
    {
      float4 pos: SV_Position;
      float3 worldPos: POSITION0;
      float2 uv: TEXTURE0;
      float3 normal: TEXTURE1;
      float4 prevPos : TEXTURE2;
      float4 curPos : TEXTURE3;
    };

    VsOutput vs_main(SceneOpaque_input input)
    {
      float4x4 mvp = FrameScope.viewProj;
      float4x4 prevMvp = FrameScope.prevViewProj;
      mvp = mul(mvp, StaticModelScope.modelTm);
      prevMvp = mul(prevMvp, StaticModelScope.modelTm);

      VsOutput output;
      output.pos = mul(mvp, float4(input.pos, 1.0));
      output.prevPos = mul(prevMvp, float4(input.pos, 1.0));
      output.curPos = output.pos;
      output.worldPos = mul(StaticModelScope.modelTm, float4(input.pos, 1.0)).xyz;
      output.uv = input.uv;
      output.normal = normalize(mul(StaticModelScope.normalTm, float4(input.normal, 0.0)).xyz);

      return output;
    }

    PackedMaterial ps_main(VsOutput input)
    {
      float3 albedo = albedoTex.Sample(modelSampler, input.uv).rgb;
      float3 metalRoughness = metalRoughnessTex.Sample(modelSampler, input.uv).rgb;
      float metal = metalRoughness.b;
      float roughness = metalRoughness.g;

      float3 viewDir = normalize(input.worldPos - FrameScope.cameraPos);
      float3 normal_ts = normalTex.Sample(modelSampler, input.uv).rgb * 2.0 - 1.0;
      float3 normal = perturbNormal(normal_ts, input.normal, viewDir, input.uv);

      UnpackedMaterial mat;
      initAlbedo(mat, albedo);
      initMetal(mat, metal);
      initRoughness(mat, roughness);
      initNormal(mat, normal);
      initMotionVector(mat, input.prevPos, input.curPos);

      return packMaterial(mat);
    }
  #end

  compile(vs_6_1, vs_main);
  compile(ps_6_1, ps_main);
};
